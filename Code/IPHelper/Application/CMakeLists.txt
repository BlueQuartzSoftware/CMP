#///////////////////////////////////////////////////////////////////////////////
#  Copyright (c) 2010, Michael A. Jackson. BlueQuartz Software
#  All rights reserved.
#  BSD License: http://www.opensource.org/licenses/bsd-license.html
#///////////////////////////////////////////////////////////////////////////////
PROJECT( IPHelperApplication )
cmake_minimum_required(VERSION 2.6)

# ---------- Setup output Directories -------------------------
SET (CMAKE_LIBRARY_OUTPUT_DIRECTORY
  ${PROJECT_BINARY_DIR}/Bin
  CACHE PATH
  "Single Directory for all Libraries"
  )

# --------- Setup the Executable output Directory -------------
SET (CMAKE_RUNTIME_OUTPUT_DIRECTORY
  ${PROJECT_BINARY_DIR}/Bin
  CACHE PATH
  "Single Directory for all Executables."
  )

# --------- Setup the Executable output Directory -------------
SET (CMAKE_ARCHIVE_OUTPUT_DIRECTORY
  ${PROJECT_BINARY_DIR}/Bin
  CACHE PATH
  "Single Directory for all static libraries."
  )


# Set the GUI type for different platforms
SET(GUI_TYPE )

# Set some Win32 Specific Settings
IF(WIN32)
 SET(GUI_TYPE WIN32)
ENDIF(WIN32)

# Set some include directories
include_directories( ${IPHelper_CODE_DIR} )
#include_directories( ${IPHelper_CODE_DIR}/MXA)
include_directories( ${IPHelper_CODE_DIR}/QtSupport )
#include_directories( ${IPHelper_CODE_DIR}/AIM )

include_directories( ${IPHelperApplication_BINARY_DIR} )
include_directories( ${IPHelper_BINARY_DIR}/MXA)
include_directories( ${IPHelper_BINARY_DIR}/QtSupport )

#-- Get the current set of source files
SET( IPHelperApplication_SRCS
    ${IPHelperApplication_SOURCE_DIR}/main.cpp
    ${IPHelperApplication_SOURCE_DIR}/IPHelperApplication.cpp
)

SET (IPHelperApplication_HDRS
    ${IPHelperApplication_SOURCE_DIR}/IPHelperApplication.h
)
IDE_SOURCE_PROPERTIES( "IPHelperApplication/GUI" "${IPHelperApplication_HDRS}" "${IPHelperApplication_SRCS}")

# --------------------------------------------------------------------
#  Get a list of the header to run 'moc' on 
# --------------------------------------------------------------------                   
SET( IPHelperApplication_MOC_HDRS
    ${IPHelperApplication_HDRS}
)

# --------------------------------------------------------------------
# The UI files As generated by Qt Designer
# --------------------------------------------------------------------
SET( IPHelperApplication_UIS
  ${IPHelperApplication_SOURCE_DIR}/UI_Files/IPHelperApplication.ui
)

# and finally any resource files
SET( IPHelperApplication_RCS ${PROJECT_RESOURCES_DIR}/icons/images/Icons.qrc)

# --------------------------------------------------------------------
# Run MOC on the files   
QT4_ADD_RESOURCES( Generated_RC_SRCS ${IPHelperApplication_RCS} )

# --------------------------------------------------------------------
# this will run uic on .ui files:
QT4_WRAP_UI( Generated_UI_HDRS ${IPHelperApplication_UIS} )

# --------------------------------------------------------------------
# and finally this will run moc:
QT4_WRAP_CPP( Generated_MOC_SRCS ${IPHelperApplication_MOC_HDRS} )

# --------------------------------------------------------------------
#-- Put the generated files into their own group for IDEs
IDE_SOURCE_PROPERTIES( "Generated" "${Generated_UI_HDRS}" "${Generated_MOC_SRCS};${Generated_RC_SRCS}")

# --------------------------------------------------------------------
# -- Finally set the Project Sources to include all the sources
SET (IPHelperApplication_PROJECT_SRCS 
    ${IPHelperApplication_SRCS}
    ${IPHelperApplication_HDRS}
    ${Generated_RC_SRCS} 
    ${Generated_UI_HDRS} 
    ${Generated_MOC_SRCS}
)


#-- Set up OS X Application Bundle properties
#-- This file have tools for OS X Bunlde creation and configuration
INCLUDE (${PROJECT_RESOURCES_DIR}/OSX_Tools/OSX_BundleTools.cmake)

IF(APPLE)
 SET(GUI_TYPE MACOSX_BUNDLE)
 ConfigureMacOSXBundlePlist(IPHelperApplication ${EXE_DEBUG_EXTENSION} 
                            ${PROJECT_RESOURCES_DIR}/icons/icns/IPHelperApplication.icns
                            ${IPHelper_VERSION} )
ENDIF (APPLE)

IF(Q_WS_WIN)
    include_directories(${PROJECT_RESOURCES_DIR}/icons/ico)
    configure_file(${PROJECT_RESOURCES_DIR}/icons/ico/IconResource.rc.in ${IPHelperApplication_BINARY_DIR}/IPHelperApplication.rc)
    SET (IPHelperApplication_PROJECT_SRCS ${IPHelperApplication_PROJECT_SRCS} ${IPHelperApplication_BINARY_DIR}/IPHelperApplication.rc) 
    IDE_SOURCE_PROPERTIES ("IPHelperApplication/ConfiguredFiles" "${IPHelperApplication_BINARY_DIR}/IPHelperApplication.rc" "")
    IDE_SOURCE_PROPERTIES( "IPHelperApplication/Configured" "${IPHelper_BINARY_DIR}/${PROJECT_PREFIX}/Common/${PROJECT_PREFIX}Version.h;${IPHelper_BINARY_DIR}/${PROJECT_PREFIX}/Common/${PROJECT_PREFIX}Configuration.h" "${IPHelper_BINARY_DIR}/${PROJECT_PREFIX}/Common/${PROJECT_PREFIX}Types.h")                
ENDIF(Q_WS_WIN)

# ---------- Build the Viewer Application  ----------------------
ADD_EXECUTABLE( IPHelperApplication ${GUI_TYPE} ${IPHelperApplication_PROJECT_SRCS} )
TARGET_LINK_LIBRARIES( IPHelperApplication
                        ${QT_LIBRARIES}
                        ${ITK_LIBRARIES}
                        AIMLib
                        QtSupport
                        MXADataModel)
                       
FindQtPlugins("qgif;qjpeg;qtiff")

InstallationSupport(IPHelperApplication ${EXE_DEBUG_EXTENSION} ${IPHelperApplication_BINARY_DIR})                
