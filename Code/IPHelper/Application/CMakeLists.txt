#///////////////////////////////////////////////////////////////////////////////
#  Copyright (c) 2010, Michael A. Jackson. BlueQuartz Software
#  All rights reserved.
#  BSD License: http://www.opensource.org/licenses/bsd-license.html
#///////////////////////////////////////////////////////////////////////////////
project(IPHelperApp)

# --------------------------------------------------------------------
# Find the ITK Package
# --------------------------------------------------------------------
IF(ITK_FOUND)
  INCLUDE(${USE_ITK_FILE})
ELSE(ITK_FOUND)
  MESSAGE(FATAL_ERROR "Cannot build without ITK.  Please set ITK_DIR.")
ENDIF(ITK_FOUND)

# ------------------------------------------------------------------------------
#  Qt 4 Section
# ------------------------------------------------------------------------------
if(QT4_FOUND)
    include( ${QT_USE_FILE} )
else()
    message(FATAL_ERROR "Qt 4 is Needed for this project.")
endif(QT4_FOUND)


# Set the GUI type for different platforms
SET(GUI_TYPE )

# Set some Win32 Specific Settings
IF(WIN32)
 SET(GUI_TYPE WIN32)
ENDIF(WIN32)

# Set some include directories
include_directories( ${PROJECT_CODE_DIR} )
include_directories( ${PROJECT_CODE_DIR}/QtSupport )

include_directories( ${IPHelperApp_BINARY_DIR} )
include_directories( ${IPHelper_BINARY_DIR}/MXA)
include_directories( ${IPHelper_BINARY_DIR}/QtSupport )

#-- Get the current set of source files
SET( IPHelperApp_SRCS
    ${IPHelperApp_SOURCE_DIR}/main.cpp
    ${IPHelperApp_SOURCE_DIR}/IPHelperApp.cpp
)

SET (IPHelperApp_HDRS
    ${IPHelperApp_SOURCE_DIR}/IPHelperApp.h
)
cmp_IDE_SOURCE_PROPERTIES( "IPHelperApp/GUI" "${IPHelperApp_HDRS}" "${IPHelperApp_SRCS}" "0")

# --------------------------------------------------------------------
#  Get a list of the header to run 'moc' on 
# --------------------------------------------------------------------                   
SET( IPHelperApp_MOC_HDRS
    ${IPHelperApp_HDRS}
)

# --------------------------------------------------------------------
# The UI files As generated by Qt Designer
# --------------------------------------------------------------------
SET( IPHelperApp_UIS
  ${IPHelperApp_SOURCE_DIR}/UI_Files/IPHelperApp.ui
)

SET( IPHelperApp_RCS ${PROJECT_RESOURCES_DIR}/icons/images/Icons.qrc)

# --------------------------------------------------------------------
# Run MOC on the files   
QT4_ADD_RESOURCES( Generated_RC_SRCS ${IPHelperApp_RCS} ${QRC_LICENSE_FILES})

# --------------------------------------------------------------------
# this will run uic on .ui files:
QT4_WRAP_UI( Generated_UI_HDRS ${IPHelperApp_UIS} )

# --------------------------------------------------------------------
# and finally this will run moc:
QT4_WRAP_CPP( Generated_MOC_SRCS ${IPHelperApp_MOC_HDRS} )

# --------------------------------------------------------------------
#-- Put the generated files into their own group for IDEs
cmp_IDE_SOURCE_PROPERTIES( "Generated" "${Generated_UI_HDRS}" "${Generated_MOC_SRCS};${Generated_RC_SRCS}" "0")

# --------------------------------------------------------------------
# -- Finally set the Project Sources to include all the sources
SET (IPHelperApp_PROJECT_SRCS 
    ${IPHelperApp_SRCS}
    ${IPHelperApp_HDRS}
    ${Generated_RC_SRCS} 
    ${Generated_UI_HDRS} 
    ${Generated_MOC_SRCS}
)


set (PluginListFile ${IPHelperApp_BINARY_DIR}/plugins.txt)
set (LibDirSearchFile ${IPHelperApp_BINARY_DIR}/libsearchdirs.txt)
file(WRITE ${PluginListFile} "")
file(WRITE ${LibDirSearchFile} "${IPHelper_BINARY_DIR}/Bin/plugins;")
# --------------------------------------------------------------------
# Get the list of Qt4 Image Format Plugins that we need
# Find all the Qt Plugins after which we should have some more variables to our disposal
FindQt4Plugins("qgif;qjpeg;qtiff" "${PluginListFile}" "${LibDirSearchFile}" )
file(READ ${PluginListFile}  QT4_COMPLETE_PLUGIN_LIST)
file(READ ${LibDirSearchFile} QT4_PLUGIN_SEARCH_DIRS)

set (COMPLETE_PLUGIN_LIST ${QT4_COMPLETE_PLUGIN_LIST})
set (PLUGIN_SEARCH_DIRS ${QT4_PLUGIN_SEARCH_DIRS})

# --------------------------------------------------------------------
# Now read the IPHelper Specific Plugins that we want for this application
file (READ ${CMP_PLUGIN_LIST_FILE} IP_COMPLETE_PLUGIN_LIST)
file (READ ${CMP_PLUGIN_SEARCHDIR_FILE} IP_PLUGIN_SEARCH_DIRS)

set (COMPLETE_PLUGIN_LIST ${COMPLETE_PLUGIN_LIST} ${IP_COMPLETE_PLUGIN_LIST})
set (PLUGIN_SEARCH_DIRS ${PLUGIN_SEARCH_DIRS} ${IP_PLUGIN_SEARCH_DIRS})


#-- Set up OS X Application Bundle properties
#-- This file have tools for OS X Bunlde creation and configuration
INCLUDE (${CMP_OSX_TOOLS_SOURCE_DIR}/OSX_BundleTools.cmake)


IF(APPLE)
 SET(GUI_TYPE MACOSX_BUNDLE)
 ConfigureMacOSXBundlePlist(IPHelperApp ${EXE_DEBUG_EXTENSION} 
                            ${PROJECT_RESOURCES_DIR}/icons/icns/IPHelperApp.icns
                            ${IPHelper_VERSION} )
ENDIF (APPLE)

IF(Q_WS_WIN)
    include_directories(${PROJECT_RESOURCES_DIR}/icons/ico)
    configure_file(${PROJECT_RESOURCES_DIR}/icons/ico/IconResource.rc.in ${IPHelperApp_BINARY_DIR}/IPHelperApp.rc)
    SET (IPHelperApp_PROJECT_SRCS ${IPHelperApp_PROJECT_SRCS} ${IPHelperApp_BINARY_DIR}/IPHelperApp.rc) 
    IDE_SOURCE_PROPERTIES ("IPHelperApp/ConfiguredFiles" "${IPHelperApp_BINARY_DIR}/IPHelperApp.rc" "")
    IDE_SOURCE_PROPERTIES( "IPHelperApp/Configured" "${IPHelper_BINARY_DIR}/${PROJECT_PREFIX}/Common/${PROJECT_PREFIX}Version.h;${IPHelper_BINARY_DIR}/${PROJECT_PREFIX}/Common/${PROJECT_PREFIX}Configuration.h" "${IPHelper_BINARY_DIR}/${PROJECT_PREFIX}/Common/${PROJECT_PREFIX}Types.h")                
ENDIF(Q_WS_WIN)

# ---------- Build the Viewer Application  ----------------------
ADD_EXECUTABLE( IPHelperApp ${GUI_TYPE} ${IPHelperApp_PROJECT_SRCS} )
TARGET_LINK_LIBRARIES( IPHelperApp
                        ${QT_LIBRARIES}
                        ${ITK_LIBRARIES}
                        AIMLib
                        QtSupport
                        MXADataModel)



cmp_InstallationSupport(IPHelperApp ${EXE_DEBUG_EXTENSION} ${IPHelperApp_BINARY_DIR} "1")                
